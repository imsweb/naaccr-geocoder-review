plugins {
    id 'java'
    id 'checkstyle'
    id 'com.github.spotbugs' version '1.6.6'
}

group = 'com.imsweb'
version = file('VERSION').text
description = 'A simple interface to review Geocoder results and select best result among several.'
sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

println "Starting build using JDK ${Runtime.version().feature()}"

repositories {
    mavenCentral()
}

dependencies {
    implementation 'au.com.bytecode:opencsv:2.4'
    implementation 'commons-io:commons-io:2.5'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.4.4'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.4.4'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.4.4'
    implementation 'com.imsweb:seerutils-gui:1.13'

    testImplementation 'junit:junit:4.12'
}

// enforce UTF-8, display the compilation warnings
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs << '-Xlint:unchecked' << '-Xlint:deprecation'
}

// the Javadoc was made way too strict in Java 8 and it's not worth the time fixing everything!
tasks.withType(Javadoc) {
    options.addStringOption('Xdoclint:none', '-quiet')
}

// customize the manifest
jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': version,
                'Implementation-Vendor': 'Information Management Services Inc.',
                'Created-By': System.properties['java.vm.version'] + ' (' + System.properties['java.vm.vendor'] + ')',
                'Built-By': System.getProperty('user.name'),
                'Built-Date': new Date(),
                'Built-JDK': System.getProperty('java.version'),
                'Automatic-Module-Name': 'com.imsweb.geocoder')
    }
    from('VERSION') {
        rename 'VERSION', 'NAACCR-GEOCODER-REVIEW-VERSION'
    }
    from('LICENSE') {
        rename 'LICENSE', 'NAACCR-GEOCODER-REVIEW-LICENSE'
    }
}

// checkstyle plugin settings
checkstyle {
    ignoreFailures = true
    configFile = project(':').file('config/checkstyle/checkstyle.xml')
}

// spotbugs plugin settings
spotbugs {
    ignoreFailures = true
    excludeFilter = project(':').file("config/spotbugs/spotbugs-exclude.xml")
}

apply from: 'gradle/release.gradle'

// Gradle wrapper, this allows to build the project without having to install Gradle...
wrapper() {
    gradleVersion = '5.1'
    distributionType = Wrapper.DistributionType.ALL
}
